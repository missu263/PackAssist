apply plugin: 'signing'
apply plugin: 'maven'

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.java.srcDirs
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

artifacts {
    archives javadocJar, sourcesJar
}

ext {
    PUBLISH_GROUP_ID = 'io.github.missu263'
    PUBLISH_VERSION = '1.3.0'
    PUBLISH_ARTIFACT_ID = "packassist"
}

ext["signing.keyId"] = ''
ext["signing.password"] = ''
ext["signing.secretKeyRingFile"] = ''
ext["ossrhUsername"] = ''
ext["ossrhPassword"] = ''

File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    println "Found secret props file, loading props"
    Properties p = new Properties()
    p.load(new FileInputStream(secretPropsFile))
    p.each { name, value ->
        ext[name] = value
    }
} else {
    println "No props file, loading env vars"
}

signing {
    sign configurations.archives
}

group = PUBLISH_GROUP_ID
archivesBaseName = PUBLISH_ARTIFACT_ID
version = PUBLISH_VERSION

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name PUBLISH_ARTIFACT_ID
                packaging 'jar'
                // optionally artifactId can be defined here
                description PUBLISH_ARTIFACT_ID
                url 'https://github.com/missu263/PackAssist'

                scm {
                    connection = 'scm:git:github.com/missu263/PackAssist.git'
                    developerConnection = 'scm:git:ssh://github.com/missu263/PackAssist.git'
                    url = 'https://github.com/missu263/PackAssist/tree/master'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'missu263'
                        name = 'nieqi'
                        email = '26388940@qq.com'
                    }
                }
            }
        }
    }
}
